Multichar_Symbols
! prefix
@D.prefix.wa-ya@

@P.prefix.m-n@
@P.prefix.wa-ya@

@R.prefix.m-n@
@R.prefix.wa-ya@

@U.prefix.wa-ya@


! infix
@P.infix.m-n2@
@P.infix.m-n@
@P.infix.wa-ya-poly@ 
@P.infix.wa-ya2@
@P.infix.wa-ya@

@D.infix.wa-ya-poly@

@R.infix.wa-ya-poly@ 

@U.infix.wa-ya-poly@ 


@D.tam1.prosp@

! number flags
@D.number.Pl@
@D.number.Plmi@
@D.number.Sg@

@P.number.Pl@
@P.number.Plmi@
@P.number.Sg@

@R.number.Pl@
@R.number.Plmi@
@R.number.Sg@

@U.number.Pl@
@U.number.Plmi@
@U.number.Sg@

! person flags
@D.person.1@
@D.person.2@
@D.person.2imp@
@D.person.3@

@P.person.1@
@P.person.2@
@P.person.2imp@
@P.person.3@

@U.person.1@
@U.person.2@
@U.person.2imp@
@U.person.3@

@P.poly.1Sg2PlO@
@P.poly.1Sg2SgO@
@P.poly.1Sg3PlO@
@P.poly.1Sg3SgO@
@P.poly.2Sg1PlO@
@P.poly.2Sg1SgO@
@P.poly.2Sg3PlO@
@P.poly.2Sg3SgO@

@R.person.1@
@R.person.2@
@R.person.2imp@
@R.person.3@

@R.poly.1Sg2PlO@
@R.poly.1Sg2SgO@
@R.poly.1Sg3PlO@
@R.poly.1Sg3SgO@
@R.poly.2Sg1PlO@
@R.poly.2Sg1SgO@
@R.poly.2Sg3PlO@
@R.poly.2Sg3SgO@

@U.poly.1Sg2PlO@
@U.poly.1Sg2SgO@
@U.poly.1Sg3PlO@
@U.poly.1Sg3SgO@
@U.poly.2Sg1PlO@
@U.poly.2Sg1SgO@
@U.poly.2Sg3PlO@
@U.poly.2Sg3SgO@

! TAM1
@P.tam1.anticip@
@P.tam1.app@
@P.tam1.cf@
@P.tam1.dubit@
@P.tam1.fut@
@P.tam1.intent@
@P.tam1.must@
@P.tam1.null@
@P.tam1.poss@
@P.tam1.prosp@
@P.tam1.prtnd@
@P.tam1.simpfut@

!Polar/Neg tags
@P.posneg.neg@

! TAM2
@D.tam2.immimp@

@P.tam2.cert@
@P.tam2.conc@
@P.tam2.cond@
@P.tam2.dec@
@P.tam2.dog@
@P.tam2.hypoth@
@P.tam2.immimp@
@P.tam2.impdel@
@P.tam2.interr@
@P.tam2.irr@
@P.tam2.polar@
@P.tam2.polarmale@
@P.tam2.poss2@
@P.tam2.pot@
@P.tam2.prohib@
@P.tam2.pst@
@P.tam2.rem@
@P.tam2.when@

! special characters, usually for morphophonemics
a2
c2
g2
i2
n2
n3
s2
y2

%<%< ! Prefix/Infix-left-edge marker
%>%> ! Suffix/Infix-right-edge marker
%_ ! Infix/Prefix insertion marker
%&
%$

LEXICON Root
wa-ya-Verbs ;
m-n-Verbs ;
polypersonal ;

LEXICON wa-ya-Verbs
@P.infix.wa-ya@ wa-ya-Reg ;
@P.infix.wa-ya2@ wa-ya-double ;

LEXICON m-n-Verbs
@P.infix.m-n@ m-n-Prefixes ;
@P.infix.m-n2@ m-n-double ;

LEXICON polypersonal
@P.infix.wa-ya-poly@ poly-wa-ya ;

LEXICON m-n-Prefixes
m-n-Stems ;

LEXICON m-n-double
@P.prefix.m-n@ m-n-iyo ;

LEXICON wa-ya-Reg
! Unitary Stems
!ǧáǧa:0_ǧaǧa     VerbPersonSuffixes ; ! for S to make O
cîga:0_cîga     VerbPersonSuffixes ; ! for S to want O
hnuda:0_hnuda VerbPersonSuffixes ; ! for S to eat their own O
agdaa:a_gdaa VerbPersonSuffixes ; ! for S to run
hnagda:0_hnagda VerbPersonSuffixesMi ; ! for S to drink their own

LEXICON wa-ya-double
@P.prefix.wa-ya@ wa-ya-manid ;

LEXICON wa-ya-manid
maní:&ma_ni VerbPersonSuffixesMi ;

LEXICON m-n-iyo
iyo:i_y2%$o      VerbPersonSuffixes ; !

LEXICON m-n-Stems
! Unitary Stems
yúda:0_y2uda      VerbPersonSuffixes ; ! for S to eat O
yagda:0_y2agda      VerbPersonSuffixesMi ; ! for S to drink O
! Bipartite Stems
!iyódąǧa:i_odąǧa VerbPersonSuffixes ; ! for S to sit down
ishdima:0_ishdima      VerbPersonSuffixesMi ; ! for S to be asleep
ca:0_i2ca      VerbPersonSuffixes ; ! progressive aux

LEXICON poly-wa-ya
uge:u_ge VerbPersonSuffixes ; ! for S to help O
apa:a_pa VerbPersonSuffixes ; ! for S to hit O
nata:na_ta  VerbPersonSuffixes ; ! for S to kick O


LEXICON VerbPersonSuffixes
@U.number.Sg@:@U.number.Sg@0                     Verbs-EndLex ;
@D.person.2imp@@U.number.Pl@:@D.person.2imp@@U.number.Pl@%>%>bi  Verbs-EndLex ;
@U.number.Pl@@U.person.2imp@:@U.number.Pl@@U.person.2imp@%>%>m  Verbs-EndLex ;

LEXICON VerbPersonSuffixesMi
@U.number.Sg@0                     Verbs-EndLex ;
@D.person.2imp@@U.number.Pl@:@D.person.2imp@@U.number.Pl@%>%>mi  Verbs-EndLex ;
@U.number.Pl@@U.person.2imp@:@U.number.Pl@@U.person.2imp@%>%>m  Verbs-EndLex ;


LEXICON Verbs-EndLex
# ;
!@P.tam2.pot@:@P.tam2.pot@%>%>kta # ;
@D.person.2imp@@D.tam2.immimp@:@D.person.2imp@@D.tam2.immimp@ Non-imp-End ;
@D.infix.wa-ya-poly@@R.person.2imp@@P.tam2.immimp@:@D.infix.wa-ya-poly@@R.person.2imp@@P.tam2.immimp@%>%>0 # ;
@D.infix.wa-ya-poly@@R.number.Sg@@R.person.2@@P.tam2.impdel@:@D.infix.wa-ya-poly@@R.number.Sg@@R.person.2@@P.tam2.impdel@%>%>cun # ;
@D.infix.wa-ya-poly@@R.number.Pl@@R.person.2@@P.tam2.impdel@:@D.infix.wa-ya-poly@@R.number.Pl@@R.person.2@@P.tam2.impdel@%>%>cun # ;

@D.infix.wa-ya-poly@@R.number.Sg@@R.person.2@@P.tam2.prohib@:@D.infix.wa-ya-poly@@R.number.Sg@@R.person.2@@P.tam2.prohib@%>%>g # ;
@D.infix.wa-ya-poly@@R.number.Pl@@R.person.2@@P.tam2.prohib@:@D.infix.wa-ya-poly@@R.number.Pl@@R.person.2@@P.tam2.prohib@%>%>g # ;


LEXICON Non-imp-End
@P.tam1.prosp@:@P.tam1.prosp@%>%>?aa Neg ;
@P.tam1.prosp@:@P.tam1.prosp@%>%>?axde Neg ;
@P.tam1.prosp@:@P.tam1.prosp@%>%>?aaraxde Neg ;
@P.tam1.simpfut@:@P.tam1.simpfut@%>%>ca Neg ;
@P.tam1.intent@:@P.tam1.intent@%>%>ha Neg ;
@P.tam1.dubit@:@P.tam1.dubit@%>%>c2ahabc'a Neg ;
@P.tam1.must@:@P.tam1.must@%>%>cazhina Neg ;
@P.tam1.must@:@P.tam1.must@%>%>cizhina Neg ;
@P.tam1.app@:@P.tam1.app@%>%>ce Neg ;
@P.tam1.poss@:@P.tam1.poss@%>%>cice Neg ;
@P.tam1.fut@:@P.tam1.fut@%>%>da2 Neg ;
@P.tam1.cf@:@P.tam1.cf@%>%>daka Neg ;
@P.tam1.anticip@:@P.tam1.anticip@%>%>dashga Neg ;
@P.tam1.prtnd@:@P.tam1.prtnd@%>%>g2uza Neg ;

Senttype ; 
Neg ;

LEXICON Neg
@P.posneg.neg@:@P.posneg.neg@%>%>s2hi Senttype ;
@P.posneg.neg@:@P.posneg.neg@%>%>s2hi # ;
Senttype ; 

LEXICON Senttype
@P.tam2.dec@:@P.tam2.dec@%>%>d # ;
@P.tam2.interr@:@P.tam2.interr@%>%>0 # ;

@P.tam2.cond@:@P.tam2.cond@%>%>c2ash # ;
@P.tam2.hypoth@:@P.tam2.hypoth@%>%>c2aash # ;
@P.tam2.irr@:@P.tam2.irr@%>%>c2ashdahwan # ;
@P.tam2.irr@:@P.tam2.irr@%>%>c2ashde # ;
@P.tam2.poss2@:@P.tam2.poss2@%>%>cin # ;
@P.tam2.pst@:@P.tam2.pst@%>%>n3 # ;
@P.tam2.when@:@P.tam2.when@%>%>cohâ # ;
@P.tam2.polar@:@P.tam2.polar@%>%>n2i # ;
@P.tam2.polarmale@:@P.tam2.polarmale@%>%>n2u # ;
@P.tam2.dog@:@P.tam2.dog@%>%>dog # ;
@P.tam2.cert@:@P.tam2.cert@%>%>dowan # ;
@P.tam2.conc@:@P.tam2.conc@%>%>gash # ;
@P.tam2.rem@:@P.tam2.rem@%>%>ge # ;
